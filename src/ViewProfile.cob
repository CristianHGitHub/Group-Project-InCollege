*> This is free-form
IDENTIFICATION DIVISION.
PROGRAM-ID. VIEWPROFILE.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT OUTFILE ASSIGN TO "../data/InCollege-Output.txt"
        ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD OUTFILE EXTERNAL.
01 OUT-REC PIC X(200).

WORKING-STORAGE SECTION.
01  WS-INPUT-PROFILE      PIC X(5000).
01  WS-FIRST-NAME         PIC X(50).
01  WS-LAST-NAME          PIC X(50).
01  WS-UNIVERSITY         PIC X(100).
01  WS-MAJOR              PIC X(100).
01  WS-GRAD-YEAR          PIC X(4).
01  WS-ABOUT-ME           PIC X(200).
01  WS-EXPERIENCE.
    05  WS-EXP-ENTRY OCCURS 3 TIMES.
        10  WS-EXP-TITLE       PIC X(50).
        10  WS-EXP-COMPANY     PIC X(50).
        10  WS-EXP-DATES       PIC X(50).
        10  WS-EXP-DESCRIPTION PIC X(200).
01  WS-EDUCATION.
    05  WS-EDU-ENTRY OCCURS 3 TIMES.
        10  WS-EDU-DEGREE      PIC X(50).
        10  WS-EDU-SCHOOL      PIC X(100).
        10  WS-EDU-YEARS       PIC X(20).

01  OUTPUT-BUFFER         PIC X(200).
01  IDX                   PIC 9 VALUE 0.
01  WS-HAS-EXPERIENCE     PIC X VALUE "N".
01  WS-HAS-EDUCATION      PIC X VALUE "N".

LINKAGE SECTION.
01  L-USERNAME            PIC X(50).
01  L-PROFILE-DATA        PIC X(5000).
01  L-MODE                PIC X(10).

PROCEDURE DIVISION USING L-USERNAME L-PROFILE-DATA L-MODE.
    MOVE L-PROFILE-DATA TO WS-INPUT-PROFILE

    *> Check if profile data is empty
    IF FUNCTION TRIM(WS-INPUT-PROFILE) = SPACES
        MOVE "You have not created a profile yet." TO OUTPUT-BUFFER
        PERFORM DUAL-OUTPUT
        GOBACK
    END-IF

    *> Break the | delimited string into fields
    UNSTRING WS-INPUT-PROFILE
        DELIMITED BY "|"
        INTO WS-FIRST-NAME
             WS-LAST-NAME
             WS-UNIVERSITY
             WS-MAJOR
             WS-GRAD-YEAR
             WS-ABOUT-ME
             WS-EXP-TITLE (1)
             WS-EXP-COMPANY (1)
             WS-EXP-DATES (1)
             WS-EXP-DESCRIPTION (1)
             WS-EXP-TITLE (2)
             WS-EXP-COMPANY (2)
             WS-EXP-DATES (2)
             WS-EXP-DESCRIPTION (2)
             WS-EXP-TITLE (3)
             WS-EXP-COMPANY (3)
             WS-EXP-DATES (3)
             WS-EXP-DESCRIPTION (3)
             WS-EDU-DEGREE (1)
             WS-EDU-SCHOOL (1)
             WS-EDU-YEARS (1)
             WS-EDU-DEGREE (2)
             WS-EDU-SCHOOL (2)
             WS-EDU-YEARS (2)
             WS-EDU-DEGREE (3)
             WS-EDU-SCHOOL (3)
             WS-EDU-YEARS (3)
    END-UNSTRING
    IF FUNCTION TRIM(L-MODE) = "SELF" OR FUNCTION TRIM(L-MODE) = SPACES
       MOVE "--- Your Profile ---" TO OUTPUT-BUFFER
       PERFORM DUAL-OUTPUT
    END-IF

    MOVE SPACES TO OUTPUT-BUFFER
    STRING "Name: " DELIMITED BY SIZE
           FUNCTION TRIM(WS-FIRST-NAME) DELIMITED BY SIZE
           " " DELIMITED BY SIZE
           FUNCTION TRIM(WS-LAST-NAME) DELIMITED BY SIZE
        INTO OUTPUT-BUFFER
    END-STRING
    PERFORM DUAL-OUTPUT

    STRING "University: " FUNCTION TRIM(WS-UNIVERSITY) INTO OUTPUT-BUFFER
    PERFORM DUAL-OUTPUT

    STRING "Major: " FUNCTION TRIM(WS-MAJOR) INTO OUTPUT-BUFFER
    PERFORM DUAL-OUTPUT

    STRING "Graduation Year: " FUNCTION TRIM(WS-GRAD-YEAR) INTO OUTPUT-BUFFER
    PERFORM DUAL-OUTPUT

    STRING "About Me: " FUNCTION TRIM(WS-ABOUT-ME) INTO OUTPUT-BUFFER
    PERFORM DUAL-OUTPUT

    MOVE "Experience:" TO OUTPUT-BUFFER
    PERFORM DUAL-OUTPUT
    MOVE "N" TO WS-HAS-EXPERIENCE
    PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > 3
        IF FUNCTION TRIM(WS-EXP-TITLE(IDX)) NOT = SPACES
            MOVE "Y" TO WS-HAS-EXPERIENCE
            STRING "  Title: " FUNCTION TRIM(WS-EXP-TITLE(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
            STRING "  Company: " FUNCTION TRIM(WS-EXP-COMPANY(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
            STRING "  Dates: " FUNCTION TRIM(WS-EXP-DATES(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
            STRING "  Description: " FUNCTION TRIM(WS-EXP-DESCRIPTION(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
        END-IF
    END-PERFORM
    IF WS-HAS-EXPERIENCE = "N"
        MOVE "  None" TO OUTPUT-BUFFER
        PERFORM DUAL-OUTPUT
    END-IF

    MOVE "Education:" TO OUTPUT-BUFFER
    PERFORM DUAL-OUTPUT
    MOVE "N" TO WS-HAS-EDUCATION
    PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > 3
        IF FUNCTION TRIM(WS-EDU-DEGREE(IDX)) NOT = SPACES
            MOVE "Y" TO WS-HAS-EDUCATION
            STRING "  Degree: " FUNCTION TRIM(WS-EDU-DEGREE(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
            STRING "  University: " FUNCTION TRIM(WS-EDU-SCHOOL(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
            STRING "  Years: " FUNCTION TRIM(WS-EDU-YEARS(IDX)) INTO OUTPUT-BUFFER
            PERFORM DUAL-OUTPUT
        END-IF
    END-PERFORM
    IF WS-HAS-EDUCATION = "N"
        MOVE "  None" TO OUTPUT-BUFFER
        PERFORM DUAL-OUTPUT
    END-IF

    *> Only print footer/separator when not invoked from SEARCH context.
    IF FUNCTION TRIM(L-MODE) NOT = "SEARCH"
        MOVE "--------------------" TO OUTPUT-BUFFER
        PERFORM DUAL-OUTPUT
        MOVE "Returning to Main Menu..." TO OUTPUT-BUFFER
        PERFORM DUAL-OUTPUT
    END-IF

    GOBACK.

*> Output routine: to both screen and OUTFILE
*> Note: OUTFILE is EXTERNAL and managed by main program
DUAL-OUTPUT.
    DISPLAY OUTPUT-BUFFER
    WRITE OUT-REC FROM OUTPUT-BUFFER
    MOVE SPACES TO OUTPUT-BUFFER
    EXIT PARAGRAPH.
