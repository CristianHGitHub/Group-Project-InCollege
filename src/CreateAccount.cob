*> This is free-form
IDENTIFICATION DIVISION.
PROGRAM-ID. CREATE-ACCOUNT.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ACCOUNT-FILE ASSIGN TO "../data/AccountRecords.txt"
        ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD ACCOUNT-FILE.
01 ACCOUNT-RECORD          PIC X(100).

WORKING-STORAGE SECTION.
COPY "AccountRecord.cpy".
01 PW-VALID                PIC X VALUE "N".
01 PW-LEN                  PIC 9(2) VALUE 0.
01 NUM-CAPS                PIC 9(2) VALUE 0.
01 NUM-DIGITS              PIC 9(2) VALUE 0.
01 NUM-SPECIAL-CHARS       PIC 9(2) VALUE 0.
01 CHAR-INDEX              PIC 9(2) VALUE 0.
01 SPECIAL-CHARS           PIC X(32) VALUE "!\'%&$#()*+,-./:;<=>?@[\]^_`{|}~".
01 HAS-UPPER               PIC X VALUE "N".
01 HAS-DIGIT               PIC X VALUE "N".
01 HAS-SPECIAL             PIC X VALUE "N".
01 CUR-CHAR                PIC X(1).
01 TEMP-COUNT              PIC 9(2) VALUE 0.
01 IS-DUPLICATE            PIC X VALUE "N".
01 EOF-ACCT                PIC X(1) VALUE "N".
01 FILE-USERNAME           PIC X(20).
01 FILE-PASSWORD           PIC X(12).

LINKAGE SECTION.
01  IN-USERNAME   PIC X(20).
01  IN-PASSWORD   PIC X(12).
01 RESPONSE       PIC X(100).
01 CREATE-STATUS  PIC X(1).

PROCEDURE DIVISION USING IN-USERNAME, IN-PASSWORD, RESPONSE, CREATE-STATUS.
    MOVE IN-USERNAME TO AR-USERNAME.

    PERFORM CHECK-DUPLICATES
    IF IS-DUPLICATE = "Y"
        MOVE SPACES TO RESPONSE
        STRING "Username " DELIMITED BY SIZE
           IN-USERNAME DELIMITED BY SPACE
           " already exists." DELIMITED BY SIZE
           INTO RESPONSE
        END-STRING
        MOVE "N" TO CREATE-STATUS
        GOBACK
    END-IF

    PERFORM VALIDATE-PASSWORD
    IF PW-VALID NOT = "Y"
        MOVE SPACES TO RESPONSE
        STRING "Invalid password for " DELIMITED BY SIZE
           AR-USERNAME DELIMITED BY SPACE
           INTO RESPONSE
        END-STRING
        MOVE "N" TO CREATE-STATUS
        GOBACK
    END-IF

    MOVE IN-PASSWORD TO AR-PASSWORD.
    MOVE SPACES TO ACCOUNT-RECORD.

    STRING AR-USERNAME DELIMITED BY SPACE
         "," DELIMITED BY SIZE
         AR-PASSWORD DELIMITED BY SPACE
         INTO ACCOUNT-RECORD
    END-STRING

    OPEN EXTEND ACCOUNT-FILE.
    WRITE ACCOUNT-RECORD
    CLOSE ACCOUNT-FILE.

    MOVE SPACES TO RESPONSE
    STRING "Account created for " DELIMITED BY SIZE
        AR-USERNAME DELIMITED BY SPACE
        INTO RESPONSE
    END-STRING
    MOVE "Y" TO CREATE-STATUS

    GOBACK.

CHECK-DUPLICATES.
    MOVE "N" TO EOF-ACCT
    MOVE "N" TO IS-DUPLICATE
    OPEN INPUT ACCOUNT-FILE
    PERFORM UNTIL EOF-ACCT = "Y"
        READ ACCOUNT-FILE
            AT END
                MOVE "Y" TO EOF-ACCT
            NOT AT END
                IF ACCOUNT-RECORD NOT = SPACES
                   UNSTRING ACCOUNT-RECORD DELIMITED BY ","
                   INTO FILE-USERNAME FILE-PASSWORD
                   END-UNSTRING
                   IF FILE-USERNAME = IN-USERNAME
                       MOVE "Y" TO IS-DUPLICATE
                       CLOSE ACCOUNT-FILE
                       EXIT PARAGRAPH
                   END-IF
                END-IF
        END-READ
    END-PERFORM
    CLOSE ACCOUNT-FILE.
    MOVE "Y" TO EOF-ACCT
    EXIT PARAGRAPH.

VALIDATE-PASSWORD.
    MOVE 0 TO PW-LEN NUM-CAPS NUM-DIGITS NUM-SPECIAL-CHARS
    MOVE "N" TO PW-VALID HAS-UPPER HAS-DIGIT HAS-SPECIAL

    IF IN-PASSWORD = SPACES
        MOVE "N" TO PW-VALID
        EXIT PARAGRAPH
    END-IF

    INSPECT IN-PASSWORD TALLYING PW-LEN FOR CHARACTERS
    IF PW-LEN < 8 OR PW-LEN > 12
        MOVE "N" TO PW-VALID
        EXIT PARAGRAPH
    END-IF

    PERFORM VARYING CHAR-INDEX FROM 1 BY 1 UNTIL CHAR-INDEX > PW-LEN
        MOVE IN-PASSWORD (CHAR-INDEX:1) TO CUR-CHAR
        IF CUR-CHAR >= "A" AND CUR-CHAR <= "Z"
            ADD 1 TO NUM-CAPS
            MOVE "Y" TO HAS-UPPER
        END-IF
        IF CUR-CHAR >= "0" AND CUR-CHAR <= "9"
            ADD 1 TO NUM-DIGITS
            MOVE "Y" TO HAS-DIGIT
        END-IF
        MOVE 0 TO TEMP-COUNT
        INSPECT SPECIAL-CHARS TALLYING TEMP-COUNT FOR ALL CUR-CHAR
        IF TEMP-COUNT > 0
            ADD 1 TO NUM-SPECIAL-CHARS
            MOVE "Y" TO HAS-SPECIAL
        END-IF
    END-PERFORM

    IF HAS-UPPER = "Y" AND HAS-DIGIT = "Y" AND HAS-SPECIAL = "Y"
        MOVE "Y" TO PW-VALID
    ELSE
        MOVE "N" TO PW-VALID
    END-IF
    EXIT PARAGRAPH.
